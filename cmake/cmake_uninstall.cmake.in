IF(NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")
	MESSAGE(FATAL_ERROR "Cannot find install manifest:"
		" \"@CMAKE_BINARY_DIR@/install_manifest.txt\"")
ENDIF()

FILE(STRINGS "@CMAKE_BINARY_DIR@/install_manifest.txt" FILES)

FOREACH(FILEPATH ${FILES})
	# handle the case when DESTDIR variable is defined
	SET(FILEPATH "$ENV{DESTDIR}${FILEPATH}")

	IF(EXISTS "${FILEPATH}" OR IS_SYMLINK "${FILEPATH}")
		MESSAGE(STATUS "Uninstalling: ${FILEPATH}")

		EXECUTE_PROCESS(COMMAND
			@CMAKE_COMMAND@ -E remove "${FILEPATH}"
		RESULT_VARIABLE
			RESULT)

		IF(RESULT)
			MESSAGE(FATAL_ERROR
				"Error when removing: \"${FILEPATH}\"")
		ENDIF()
	ENDIF()

	SET(OLD_DIRPATH "${FILEPATH}")
	GET_FILENAME_COMPONENT(DIRPATH "${OLD_DIRPATH}" DIRECTORY)

	WHILE(NOT DIRPATH STREQUAL OLD_DIRPATH)
		IF(EXISTS "${DIRPATH}")
			FILE(GLOB FILES_IN_DIR "${DIRPATH}/*")
			LIST(LENGTH FILES_IN_DIR NO_FILES_IN_DIR)

			IF(NOT NO_FILES_IN_DIR EQUAL 0)
				BREAK()
			ENDIF()

			MESSAGE(STATUS "Removing: ${DIRPATH}")

			EXECUTE_PROCESS(COMMAND
				@CMAKE_COMMAND@ -E remove_directory ${DIRPATH}
			RESULT_VARIABLE
				RESULT)
		ENDIF()

		SET(OLD_DIRPATH "${DIRPATH}")
		GET_FILENAME_COMPONENT(DIRPATH "${OLD_DIRPATH}" DIRECTORY)
	ENDWHILE()
ENDFOREACH()
