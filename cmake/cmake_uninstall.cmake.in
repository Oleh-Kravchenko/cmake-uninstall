IF(NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")
	MESSAGE(FATAL_ERROR "Cannot find install manifest:"
		" \"@CMAKE_BINARY_DIR@/install_manifest.txt\"")
ENDIF()

FILE(STRINGS "@CMAKE_BINARY_DIR@/install_manifest.txt" FILES)

IF(DEFINED ENV{DESTDIR})
	SET(UNINSTALL_PATH "$ENV{DESTDIR}")
ELSE()
	SET(UNINSTALL_PATH "@CMAKE_INSTALL_PREFIX@")
ENDIF()

FOREACH(FILEPATH ${FILES})
	# handle the case when DESTDIR variable is defined
	SET(FILEPATH "$ENV{DESTDIR}${FILEPATH}")

	IF(EXISTS "${FILEPATH}" OR IS_SYMLINK "${FILEPATH}")
		MESSAGE(STATUS "Uninstalling: ${FILEPATH}")

		EXECUTE_PROCESS(COMMAND
			@CMAKE_COMMAND@ -E remove "${FILEPATH}"
		RESULT_VARIABLE
			RESULT)

		IF(RESULT)
			MESSAGE(FATAL_ERROR
				"Error when removing: \"${FILEPATH}\"")
		ENDIF()
	ENDIF()

	GET_FILENAME_COMPONENT(DIRPATH "${FILEPATH}" DIRECTORY)

	WHILE(NOT DIRPATH STREQUAL UNINSTALL_PATH)
		IF(EXISTS "${DIRPATH}")
			FILE(GLOB FILES_IN_DIR "${DIRPATH}/*")
			LIST(LENGTH FILES_IN_DIR NO_FILES_IN_DIR)

			IF(NOT NO_FILES_IN_DIR EQUAL 0)
				BREAK()
			ENDIF()

			MESSAGE(STATUS "Removing: ${DIRPATH}")

			EXECUTE_PROCESS(COMMAND
				@CMAKE_COMMAND@ -E remove_directory ${DIRPATH}
			RESULT_VARIABLE
				RESULT)

			IF(RESULT)
				MESSAGE(FATAL_ERROR
					"Error when removing: \"${DIRPATH}\"")
			ENDIF()
		ENDIF()

		GET_FILENAME_COMPONENT(DIRPATH "${DIRPATH}" DIRECTORY)
	ENDWHILE()
ENDFOREACH()
